allprojects {
	project.apply plugin: "com.diffplug.spotless"
	spotless {
		java {
			// Normally this isn't necessary, but we have Java sources in
			// non-standard places
			target '**/*.java'

			importOrder(
				'java|javax',
				'com.carrotsearch|com.google|com.fasterxml|org.apache|org.awaitility|org.hamcrest|org.junit|org.bouncycastle',
				'org.opensearch',
				'',
				'\\#java|\\#org.apache|\\#org.hamcrest|\\#org.opensearch|\\#'
			)
			removeUnusedImports()
			eclipse().configFile rootProject.file('formatter/formatterConfig.xml')
			trimTrailingWhitespace()
			endWithNewline();
			custom 'Replace illegal HttpStatus import w/ correct one', {
				// e.g., replace org.apache.hc.core5.http.HttpStatus with org.apache.http.HttpStatus
				it.replaceAll('org.apache.hc.core5.http.HttpStatus', 'org.apache.http.HttpStatus')
			}

			// See DEVELOPER_GUIDE.md for details of when to enable this.
			if (System.getProperty('spotless.paddedcell') != null) {
				paddedCell()
			}
		}
		format 'misc', {
			target '*.md', '*.gradle', '**/*.json', '**/*.yaml', '**/*.yml', '**/*.svg'

			trimTrailingWhitespace()
			endWithNewline()
		}
	}
}
