/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'checkstyle'
    id "com.gorylenko.gradle-git-properties" version "2.3.2"
    id 'org.gradle.crypto.checksum' version '1.1.0'

    // Plugin prints gradle task graph, use following command: ./gradlew tiTree build
    id 'org.barfuin.gradle.taskinfo' version '1.0.5'

    id "nebula.ospackage" version "9.0.0"
    id "com.google.osdetector" version "1.7.0"
    id "org.gradle.test-retry" version "1.3.1"
    id "com.github.spotbugs" version "5.0.13"
}
import org.gradle.crypto.checksum.Checksum

import java.text.SimpleDateFormat


repositories {
    mavenLocal()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

ext {
    isSnapshot = "true" == System.getProperty("build.snapshot", "true")
    opensearch_version = System.getProperty("opensearch.version", "1.3.15-SNAPSHOT")
    buildVersionQualifier = System.getProperty("build.version_qualifier", "")
    version_tokens = opensearch_version.tokenize('-')
    opensearch_build = version_tokens[0] + '.0'
    kafka_version  = '3.5.1'

    if (buildVersionQualifier) {
        opensearch_build += "-${buildVersionQualifier}"
        opensearch_build_nosnapshot = opensearch_build
    }
    if (isSnapshot) {
        opensearch_build += "-SNAPSHOT"
    }
}

configurations.all {
    resolutionStrategy {
        force 'commons-codec:commons-codec:1.14'
        force 'org.apache.santuario:xmlsec:2.3.4'
        force 'org.cryptacular:cryptacular:1.2.4'
        force 'net.minidev:json-smart:2.4.10'
        force 'commons-cli:commons-cli:1.3.1'
        force 'org.apache.httpcomponents:httpcore:4.4.12'
        force "org.apache.commons:commons-lang3:3.4"
        force "org.springframework:spring-core:5.3.28"
        force "org.springframework:spring-expression:5.3.28"
        force "com.google.guava:guava:32.1.1-jre"
        force "com.fasterxml.woodstox:woodstox-core:6.4.0"
        force "org.scala-lang:scala-library:2.13.9"
        force "org.apache.bcel:bcel:6.6.0" // This line should be removed once Spotbugs is upgraded to 4.7.4
        force "org.xerial.snappy:snappy-java:1.1.10.5"
        force "org.apache.zookeeper:zookeeper:3.9.1"
        force "ch.qos.logback:logback-classic:1.2.13"
    }
}

//create source set 'integrationTest'
//add classes from the main source set to the compilation and runtime classpaths of the integrationTest
sourceSets {
    integrationTest {
        java {
            srcDir file ('src/integrationTest/java')
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
        resources {
            srcDir file('src/integrationTest/resources')
        }
        processIntegrationTestResources {
            duplicatesStrategy(DuplicatesStrategy.INCLUDE)
        }
    }
}

//add new task that runs integration tests
task integrationTest(type: Test) {
    doFirst {
        // Only run resources tests on resource-test CI environments or locally
        if (System.getenv('CI_ENVIRONMENT') != 'resource-test' && System.getenv('CI_ENVIRONMENT') != null) {
            exclude '**/ResourceFocusedTests.class'
        }
        // Only run with retries while in CI systems
        if (System.getenv('CI_ENVIRONMENT') == 'normal') {
            retry {
                failOnPassedAfterRetry = false
                maxRetries = 2
                maxFailures = 10
            }
        }
    }
    description = 'Run integration tests.'
    group = 'verification'
    systemProperty "java.util.logging.manager", "org.apache.logging.log4j.jul.LogManager"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    //run the integrationTest task after the test task
    shouldRunAfter test
    jacoco {
        excludes = [
                "com.sun.jndi.dns.*",
                "com.sun.security.sasl.gsskerb.*",
                "java.sql.*",
                "javax.script.*",
                "org.jcp.xml.dsig.internal.dom.*",
                "sun.nio.cs.ext.*",
                "sun.security.ec.*",
                "sun.security.jgss.*",
                "sun.security.pkcs11.*",
                "sun.security.smartcardio.*",
                "sun.util.resources.provider.*"
        ]
    }
}

//run the integrationTest task before the check task
check.dependsOn integrationTest

dependencies {

    modules {
        module("org.bouncycastle:bcprov-jdk15on") {
            replacedBy("org.bouncycastle:bcprov-jdk15to18", "the jdk15on artifacts are not supported anymore")
        }
    }

    implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    implementation "org.opensearch.plugin:transport-netty4-client:${opensearch_version}"
    implementation "org.opensearch.client:opensearch-rest-high-level-client:${opensearch_version}"
    implementation 'com.google.guava:guava:32.1.1-jre'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'commons-cli:commons-cli:1.3.1'
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.75'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'org.ldaptive:ldaptive:1.2.3'
    implementation 'org.apache.httpcomponents:httpclient-cache:4.5.13'
    implementation 'io.jsonwebtoken:jjwt-api:0.10.8'
    implementation("org.apache.cxf:cxf-rt-rs-security-jose:3.5.5") {
        exclude(group: 'jakarta.activation', module: 'jakarta.activation-api')
    }
    implementation 'com.github.wnameless:json-flattener:0.5.0'
    implementation 'com.flipkart.zjsonpatch:zjsonpatch:0.4.4'
    implementation "org.apache.kafka:kafka-clients:${kafka_version}"
    implementation 'com.onelogin:java-saml:2.5.0'
    implementation ('org.opensaml:opensaml-saml-impl:3.4.5') {
        exclude(group: 'org.apache.velocity', module: 'velocity')
    }
    implementation 'commons-lang:commons-lang:2.4'
    implementation 'commons-collections:commons-collections:3.2.2'
    implementation 'com.jayway.jsonpath:json-path:2.9.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.10.8'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.10.8'
    runtimeOnly 'org.apache.commons:commons-text:1.10.0'
    runtimeOnly 'org.xerial.snappy:snappy-java:1.1.10.5'

    testImplementation "org.opensearch.plugin:reindex-client:${opensearch_version}"
    testImplementation "org.opensearch:opensearch-ssl-config:${opensearch_version}"
    testImplementation "org.opensearch.plugin:percolator-client:${opensearch_version}"
    testImplementation "org.opensearch.plugin:lang-mustache-client:${opensearch_version}"
    testImplementation "org.opensearch.plugin:parent-join-client:${opensearch_version}"
    testImplementation "org.opensearch.plugin:aggs-matrix-stats-client:${opensearch_version}"
    testImplementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    testImplementation 'commons-io:commons-io:2.7'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.apache.httpcomponents:fluent-hc:4.5.13'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'org.springframework.kafka:spring-kafka-test:2.9.10'
    testImplementation 'javax.servlet:servlet-api:2.5'
    testImplementation 'com.unboundid:unboundid-ldapsdk:4.0.9'
    testImplementation 'com.github.stephenc.jcip:jcip-annotations:1.0-1'
    testImplementation "org.apache.kafka:kafka_2.13:${kafka_version}"
    testImplementation "org.apache.kafka:kafka_2.13:${kafka_version}:test"
    testImplementation "org.apache.kafka:kafka-clients:${kafka_version}:test"
    compileOnly "org.opensearch:opensearch:${opensearch_version}"

    integrationTestCompileOnly "org.opensearch:opensearch:${opensearch_version}"
    integrationTestImplementation "org.opensearch.plugin:reindex-client:${opensearch_version}"
    integrationTestImplementation "org.opensearch:opensearch-ssl-config:${opensearch_version}"
    integrationTestImplementation "org.opensearch.plugin:percolator-client:${opensearch_version}"
    integrationTestImplementation "org.opensearch.plugin:lang-mustache-client:${opensearch_version}"
    integrationTestImplementation "org.opensearch.plugin:parent-join-client:${opensearch_version}"
    integrationTestImplementation "org.opensearch.plugin:aggs-matrix-stats-client:${opensearch_version}"
    integrationTestImplementation "com.google.guava:guava:32.1.1-jre"
    integrationTestImplementation "org.apache.commons:commons-lang3:3.4"
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    integrationTestImplementation 'io.jsonwebtoken:jjwt-api:0.10.8'
    integrationTestImplementation "org.opensearch.client:opensearch-rest-high-level-client:${opensearch_version}"
    integrationTestImplementation "org.opensearch.plugin:transport-netty4-client:${opensearch_version}"

    integrationTestRuntimeOnly 'org.greenrobot:eventbus:3.2.0'
    integrationTestRuntimeOnly 'com.flipkart.zjsonpatch:zjsonpatch:0.4.4'
    integrationTestRuntimeOnly 'commons-lang:commons-lang:2.4'
    integrationTestRuntimeOnly 'org.ldaptive:ldaptive:1.2.3'
    integrationTestRuntimeOnly 'commons-collections:commons-collections:3.2.2'

    //integration test framework:
    integrationTestImplementation('com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.8.1') {
        exclude(group: 'junit', module: 'junit')
    }
    integrationTestImplementation 'junit:junit:4.13.2'
    integrationTestImplementation "org.opensearch.plugin:reindex-client:${opensearch_version}"
    integrationTestImplementation "org.opensearch.plugin:percolator-client:${opensearch_version}"
    integrationTestImplementation 'commons-io:commons-io:2.14.0'
    integrationTestImplementation "org.apache.logging.log4j:log4j-core:2.17.1"
    integrationTestImplementation "org.apache.logging.log4j:log4j-jul:2.17.1"
    integrationTestImplementation 'org.hamcrest:hamcrest:2.2'
    integrationTestImplementation "org.bouncycastle:bcpkix-jdk15to18:1.75"
    integrationTestImplementation "org.bouncycastle:bcutil-jdk15to18:1.75"
    integrationTestImplementation('org.awaitility:awaitility:4.2.0') {
        exclude(group: 'org.hamcrest', module: 'hamcrest')
    }
    integrationTestImplementation 'com.unboundid:unboundid-ldapsdk:4.0.14'
    integrationTestImplementation "org.apache.httpcomponents:httpclient-cache:4.5.14"
    integrationTestImplementation "org.apache.httpcomponents:httpclient:4.5.14"
    integrationTestImplementation "org.apache.httpcomponents:fluent-hc:4.5.13"
    integrationTestImplementation "org.apache.httpcomponents:httpcore:4.4.16"
    integrationTestImplementation "org.apache.httpcomponents:httpasyncclient:4.1.5"
}

group = 'org.opensearch'
version = opensearch_build
description = 'OpenSearch Security'


java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

tasks.register('testsJar', Jar) {
    archiveClassifier = 'tests'
    from(sourceSets.test.output)
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            artifact(testsJar)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.warnings = false
}

static def getTimestamp() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

static def gitCommitId() {
    def cmd = "git rev-parse HEAD"
    def proc = cmd.execute()
    return proc.text.trim()
}

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Created-By": "Gradle ${gradle.gradleVersion}",
                "Build-Jdk": "${System.properties['java.version']}",
                "Implementation-Title": "OpenSearch Security",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor-Id": "org.opensearch",
                "Implementation-URL": "https://github.com/opensearch-project/security",
                "Build-Time": getTimestamp(),
                "Built-By": "OpenSearch Security Plugin",
                "git-sha1": gitCommitId()
        )
    }

    libsDirName = '.'
    into '', {
        from 'NOTICE.txt', "THIRD-PARTY.txt", "LICENSE"
    }
    processResources {
        exclude("KEYS")
    }

}

testsJar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Created-By": "Gradle ${gradle.gradleVersion}",
                "Build-Jdk": "${System.properties['java.version']}",
                "Implementation-Title": "OpenSearch Security",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor-Id": "org.opensearch",
                "Implementation-URL": "https://github.com/opensearch-project/security",
                "Build-Time": getTimestamp(),
                "Built-By": "OpenSearch Security Plugin",
                "git-sha1": gitCommitId()
        )
    }

    libsDirName = '.'
}

spotbugs {
    includeFilter = file('spotbugs-include.xml')
}

spotbugsTest {
    enabled = false
}

test {
    maxParallelForks = 3
    jvmArgs += "-Xmx3072m"
    if (JavaVersion.current() > JavaVersion.VERSION_1_8) {
        jvmArgs += "--add-opens=java.base/java.io=ALL-UNNAMED"
    }
    retry {
        failOnPassedAfterRetry = false
        maxFailures = 30
        maxRetries = 5
    }
}

gitProperties {
    keys = [
            'git.branch',
            'git.build.version',
            'git.closest.tag.commit.count',
            'git.closest.tag.name',
            'git.commit.id',
            'git.commit.id.abbrev',
            'git.commit.id.describe',
            'git.commit.message.full',
            'git.commit.message.short',
            'git.commit.time',
            'git.dirty',
            'git.remote.origin.url',
            'git.tags',
            'git.total.commit.count'
    ]
}

// copied from: org.opensearch.gradle.dependencies.CompileOnlyResolvePlugin
project.getConfigurations().all(configuration -> {
    if (configuration.getName().equals(JavaPlugin.COMPILE_ONLY_CONFIGURATION_NAME)) {
        NamedDomainObjectProvider<Configuration> resolvableCompileOnly = project.getConfigurations().register('resolveableCompileOnly');
        resolvableCompileOnly.configure((c) -> {
            c.setCanBeResolved(true);
            c.setCanBeConsumed(false);
            c.extendsFrom(configuration);
        });
    }
});


task bundle(dependsOn: jar, type: Zip) {
    from configurations.runtimeClasspath - project.configurations.getByName('resolveableCompileOnly')
    from project.jar
    from 'plugin-security.policy'
    from 'plugin-descriptor.properties'
    from('securityconfig') {
        into 'securityconfig/'
    }
    from('tools') {
        into 'tools/'
    }
}

task bundleSecurityAdminStandalone(dependsOn: jar, type: Zip) {
    archiveClassifier = 'securityadmin-standalone'
    from(configurations.runtimeClasspath) {
        into 'deps/'
    }
    from(project.jar) {
        into 'deps/'
    }
    from('tools') {
        into 'tools/'
    }
    from('securityconfig') {
        into 'deps/securityconfig'
    }
}
task bundleSecurityAdminStandaloneTarGz(dependsOn: jar, type: Tar) {
    archiveClassifier = 'securityadmin-standalone'
    archiveExtension = 'tar.gz'
    compression = Compression.GZIP
    from(configurations.runtimeClasspath) {
        into 'deps/'
    }
    from(project.jar) {
        into 'deps/'
    }
    from('tools') {
        into 'tools/'
    }
    from('securityconfig') {
        into 'deps/securityconfig'
    }
}

task createPluginDescriptor() {
    List<String> descriptorProperties = [
            "description=Provide access control related features for OpenSearch",
            "version=${version}",
            "name=opensearch-security",
            "classname=org.opensearch.security.OpenSearchSecurityPlugin",
            "java.version=${java.targetCompatibility}",
            "opensearch.version=${version_tokens[0]}",
    ]

    new File("plugin-descriptor.properties").text = descriptorProperties.join ("\n")
}
bundle.doLast() {
    new File("plugin-descriptor.properties").delete()
}

tasks.assemble.dependsOn(bundle)
tasks.bundle.dependsOn(createPluginDescriptor)
tasks.assemble.dependsOn(bundleSecurityAdminStandalone)
tasks.assemble.dependsOn(bundleSecurityAdminStandaloneTarGz)

clean {
    delete 'data/'
}


task createChecksums(type: Checksum) {
    files = bundle.outputs.files
    outputDir = new File(project.buildDir, "distributions")
    algorithm = Checksum.Algorithm.SHA512
}
tasks.assemble.finalizedBy(createChecksums)


jacoco {
    reportsDirectory = file("$buildDir/jacoco")
}
jacocoTestReport {
    reports {
        xml.required = true
    }
}

tasks.test.finalizedBy(jacocoTestReport)  // report is always generated after tests run
tasks.jacocoTestReport.dependsOn(test) // tests are required to run before generating the report

checkstyle {
    configFile file("config/checkstyle/sun_checks.xml")
}

tasks.withType(Checkstyle) {
    reports {
        showViolations false
        ignoreFailures = true
        xml.required = true
        html.required = true
    }
}

buildRpm {
    arch = 'NOARCH'
    addParentDirs = false
    archiveName "${packageName}-${version}.rpm"
}
buildDeb {
    arch = 'all'
    archiveName "${packageName}-${version}.deb"
}

// updateVersion: Task to auto increment to the next development iteration
task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
        // String tokenization to support -SNAPSHOT
        ant.replaceregexp(match: opensearch_version.tokenize('-')[0], replace: newVersion.tokenize('-')[0], flags:'g', byline:true) {
            fileset(dir: projectDir) {
                // Include the required files that needs to be updated with new Version
                include(name: ".github/workflows/plugin_install.yml")
            }
        }
        ant.replaceregexp(file:'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags:'g', byline:true)
    }
}
