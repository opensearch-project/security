/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "1.0.0-beta1")
        log4j_version = "2.11.1"
        jjwt_version = "0.10.5"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = uri('https://plugins.gradle.org/m2/')
        }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
    }
}

plugins {
    id "nebula.ospackage" version "8.5.6"
    id 'java'
    id 'maven-publish'
    id "com.gorylenko.gradle-git-properties" version "2.2.4"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
}

test{
    workingDir = '.'
}

apply plugin: 'opensearch.opensearchplugin'
opensearchplugin {
    name 'opensearch-security'
    description 'OpenSearch Security for Elasticsearch'
    classname 'com.amazon.opendistroforelasticsearch.security.OpenDistroSecurityPlugin'
}

group = 'com.amazon.opendistroforelasticsearch'
version = '1.0.0.0-beta1'
java.sourceCompatibility = JavaVersion.VERSION_1_8

bundlePlugin {
    from("securityconfig") {
        into "securityconfig"
    }
    from("tools") {
        into "tools"
    }
    from("plugin-security.policy") {
        into "."
    }
}

apply plugin: 'distribution'
distributions {
    custom {
        distributionBaseName = "opensearch-security-${version}-securityadmin-standalone"
        contents {
            into('./tools/') {
                from 'tools'
            }
            into('./deps/securityconfig') {
                from 'securityconfig'
            }
        }
    }
    customDistZip.setArchiveName("opensearch-security-${version}-securityadmin-standalone.zip")
    customDistTar.setArchiveName("opensearch-security-${version}-securityadmin-standalone.tar.gz")
}

customDistTar{
    compression = Compression.GZIP
    archiveExtension = "tar.gz"
}

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}

configurations.all {
    if (it.state != Configuration.State.UNRESOLVED) return
    resolutionStrategy {
        force 'commons-codec:commons-codec:1.14'
        force 'org.slf4j:slf4j-api:1.7.26'
        force 'org.apache.httpcomponents:httpclient:4.5.10'
        force 'com.google.code.findbugs:jsr305:3.0.2'
        force 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
        force 'com.fasterxml.jackson.core:jackson-core:2.11.2'
        force 'commons-cli:commons-cli:1.3.1'
        force 'commons-logging:commons-logging:1.2'
        force 'com.google.guava:guava:25.1-jre'
        force 'org.bouncycastle:bcprov-jdk15on:1.67'
        force 'org.apache.commons:commons-lang3:3.4'
        force 'org.apache.santuario:xmlsec:2.2.0'
        force 'com.fasterxml.woodstox:woodstox-core:6.2.1'
        force 'io.netty:netty-buffer:4.1.49.Final'
        force 'io.netty:netty-common:4.1.49.Final'
        force 'io.netty:netty-handler:4.1.49.Final'
        force 'io.netty:netty-transport:4.1.49.Final'
        force 'com.fasterxml.jackson.core:jackson-annotations:2.11.2'
        force 'net.sf.jopt-simple:jopt-simple:5.0.4'
        force 'org.scala-lang:scala-library:2.12.10'
        force 'org.scala-lang:scala-reflect:2.12.10'
        force 'org.cryptacular:cryptacular:1.1.4'
        force 'org.apache.httpcomponents:httpcore:4.4.12'
        exclude group: "org.elasticsearch", module: "securemock"
    }
}

configurations.testCompile {
    if (it.state != Configuration.State.UNRESOLVED) return
    resolutionStrategy {
        force 'org.apache.kafka:kafka-clients:2.0.1:test'
    }
}

dependencies {
    compile("org.opensearch.plugin:transport-netty4-client:${opensearch_version}") {
        exclude group: 'org.opensearch', module: 'jna'
        exclude group: 'com.vividsolutions', module: 'jts'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }
    compile 'com.google.guava:guava:25.1-jre'
    compile 'com.google.errorprone:error_prone_annotations:2.1.3'
    compile 'org.greenrobot:eventbus:3.2.0'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'org.bouncycastle:bcprov-jdk15on:1.67'
    compile('com.fasterxml.jackson.core:jackson-databind:2.11.2') {
        exclude group: 'com.fasterxml.jackson.core', module: 'ackson-core'
    }
    compile("org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}") {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }
    compile('org.ldaptive:ldaptive:1.2.3') {
        exclude group: 'commons-cli', module: 'commons-cli'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compile('org.apache.httpcomponents:httpclient-cache:4.5.3') {
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
    }
    compile("org.opensearch.client:opensearch-rest-high-level-client:${opensearch_version}") {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile "io.jsonwebtoken:jjwt-api:${jjwt_version}"
    compile('org.apache.cxf:cxf-rt-rs-security-jose:3.4.0') {
        exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
    }
    compile 'com.github.wnameless:json-flattener:0.5.0'
    compile('com.flipkart.zjsonpatch:zjsonpatch:0.4.4') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    compile('org.apache.kafka:kafka-clients:2.5.0') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compile('com.onelogin:java-saml:2.5.0') {
        exclude group: 'org.codehaus.woodstox', module: 'woodstox-core-asl'
        exclude group: 'joda-time', module: 'joda-time'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile('org.opensaml:opensaml-saml-impl:3.4.5') {
        exclude group: 'joda-time', module: 'joda-time'
        exclude group: 'commons-collections', module: 'commons-collections'
    }
    compile 'commons-collections:commons-collections:3.2.2'
    compile 'com.jayway.jsonpath:json-path:2.4.0'
    compile('org.apache.httpcomponents:httpclient:4.5.3') {
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
    }
    compile("io.jsonwebtoken:jjwt-jackson:${jjwt_version}") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    compile 'org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1'
    compile 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    compile 'jakarta.jws:jakarta.jws-api:2.1.0'
    compile('jakarta.xml.soap:jakarta.xml.soap-api:1.4.2') {
        exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
    }
    compile('jakarta.xml.ws:jakarta.xml.ws-api:2.3.3') {
        exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
    }
    compile 'org.jboss.spec.javax.rmi:jboss-rmi-api_1.0_spec:1.0.6.Final'
    compile "org.opensearch.plugin:lang-mustache-client:${opensearch_version}"
    compile "org.opensearch.plugin:parent-join-client:${opensearch_version}"
    compile "org.opensearch.plugin:aggs-matrix-stats-client:${opensearch_version}"
    compileOnly "org.apache.logging.log4j:log4j-core:${log4j_version}"
    compileOnly "org.opensearch:opensearch:${opensearch_version}"
    compileOnly 'io.netty:netty-tcnative:2.0.25.Final'
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwt_version}"
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:${jjwt_version}") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    runtimeOnly 'com.sun.xml.messaging.saaj:saaj-impl:1.5.2'
    runtimeOnly 'org.jvnet.staxex:stax-ex:1.8.3'
    testCompileOnly 'org.hamcrest:hamcrest-all:1.3'
    testCompileOnly 'org.springframework.kafka:spring-kafka-test:2.5.4.RELEASE'
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.apache.httpcomponents:fluent-hc:4.5.3'
    testImplementation("org.opensearch.plugin:reindex-client:${opensearch_version}") {
        exclude group: 'org.opensearch', module: 'opensearch-ssl-config'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation "org.opensearch:opensearch-ssl-config:${opensearch_version}"
    testImplementation "org.opensearch.plugin:percolator-client:${opensearch_version}"
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.9.0'
    testImplementation 'org.objenesis:objenesis:2.6'
    testImplementation 'javax.servlet:servlet-api:2.5'
    testImplementation 'com.unboundid:unboundid-ldapsdk:4.0.9'
    testImplementation 'com.github.stephenc.jcip:jcip-annotations:1.0-1'
}

tasks.register('testsJar', Jar) {
    archiveClassifier = 'tests'
    from(sourceSets.test.output)
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            artifact(testsJar)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

gitProperties {
    gitPropertiesName = "git.properties"
    gitPropertiesResourceDir = "$buildDir/classes"
    keys = ['git.branch', 'git.build.time', 'git.build.version', 'git.closest.tag.commit.count', 'git.closest.tag.name',
            'git.commit.id', 'git.commit.id.abbrev', 'git.commit.id.describe', 'git.commit.id.describe-short',
            'git.commit.message.full', 'git.commit.message.short', 'git.commit.time', 'git.dirty',
            'git.remote.origin.url', 'git.tags', 'git.total.commit.count', 'gitPropertiesResourceDir']
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.remove("-Werror")
            options.compilerArgs.remove("-Xdoclint:all")
            options.compilerArgs.remove("-Xdoclint:-missing")
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            options.warnings(false)
        }
    }
}

javadoc.enabled(false)
checkstyleMain.enabled(false)
checkstyleTest.enabled(false)
filepermissions.enabled(false)
forbiddenPatterns.enabled(false)
validateMavenPom.enabled(false)
validateNebulaPom.enabled(false)
testingConventions.enabled(false)
forbiddenApisMain.enabled(false)
forbiddenApisTest.enabled(false)
forbiddenApisTest.enabled(false)
dependencyLicenses.enabled(false)
thirdPartyAudit.enabled(false)
loggerUsageCheck.enabled(false)
licenseHeaders.enabled(false)






// Uses Gradle to build RPMs since that's what we use for the other plugins. When all you have is a hammer...
// To prevent conflicts with maven build under build/
buildDir = 'gradle-build'

ext {
    opensearchVersion = '1.0.0.0-beta1'
    isSnapshot = "true" == System.getProperty("build.snapshot", "true")
}

// Increment the final digit when there's a new plugin versions for the same opensearch version
// Reset the final digit to 0 when upgrading to a new opensearch version
version = "${opensearchVersion}" + (isSnapshot ? "-SNAPSHOT" : "")

ospackage {
    packageName = "opensearch-security"
    release = isSnapshot ? "0.1" : '1'
    version = "${project.version}"

    into '/usr/share/opensearch/plugins'
    from(zipTree(bundlePlugin.archivePath)) {
        into "opensearch-security"
        permissionGroup 'opensearch'
    }

    user 'root'
    permissionGroup 'root'

    requires('opensearch-oss', "${opensearch_version}", EQUAL)
    packager = 'Amazon'
    vendor = 'Amazon'
    os = 'LINUX'
    prefix '/usr'

    license 'ASL-2.0'
    maintainer 'OpenDistro for Elasticsearch Team <opendistro@amazon.com>'
    url 'https://opendistro.github.io/for-elasticsearch/downloads.html'
    summary '''
     OpenSearch Security plugin. 
     Reference documentation can be found at https://opendistro.github.io/for-elasticsearch-docs/.
     '''.stripIndent().replace('\n', ' ').trim()

    //TODO: Would be better if the install_demo_configuration.sh script is marked executable in the upstream plugin instead of running bash manually here
    postInstall "exec /bin/bash /usr/share/opensearch/plugins/opensearch-security/tools/install_demo_configuration.sh -y -i -s"
}

buildRpm {
    arch = 'NOARCH'
    addParentDirs = false
    finalizedBy 'renameRpm'
    task renameRpm(type: Copy) {
        from("$buildDir/distributions")
        into("$buildDir/distributions")
        include archiveName
        rename archiveName, "${packageName}-${version}.rpm"
        doLast { delete file("$buildDir/distributions/$archiveName") }
    }
}

buildDeb {
    arch = 'all'
    finalizedBy 'renameDeb'
    task renameDeb(type: Copy) {
        from("$buildDir/distributions")
        into("$buildDir/distributions")
        include archiveName
        rename archiveName, "${packageName}-${version}.deb"
        doLast { delete file("$buildDir/distributions/$archiveName") }
    }
}
